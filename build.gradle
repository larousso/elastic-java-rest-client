buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
    id "maven"
    id "maven-publish"
    id "java"
    id 'ch.netzwerg.release' version '1.2.5'
}

sourceCompatibility = 1.8

def akkaVersion = "2.5.3"
def scalaVersion = "2.12"
def scm = "https://github.com/larousso/elastic-java-rest-client"

group 'com.adelegue'

jar {
    baseName = 'elastic-java-rest-client'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile('io.vavr:vavr:0.9.0')
    compile("com.typesafe.akka:akka-stream_${scalaVersion}:${akkaVersion}")
    compile("org.reactivecouchbase.json:json-lib:1.0.0")
    compile("org.elasticsearch.client:rest:5.5.0")
    testCompile("com.typesafe.akka:akka-testkit_${scalaVersion}:${akkaVersion}")
    testCompile("org.elasticsearch:elasticsearch:5.5.0")
    testCompile("org.elasticsearch.plugin:transport-netty4-client:5.5.0")
    testCompile("org.elasticsearch.plugin:reindex-client:5.5.0")
    testCompile("org.slf4j:slf4j-api:1.7.25")
    testCompile("org.assertj:assertj-core:3.6.2")
    testCompile("junit:junit:4.12")
    testCompile("org.assertj:assertj-core:3.5.2")
    testCompile("org.apache.logging.log4j:log4j-api:2.8.2")
    testCompile("org.apache.logging.log4j:log4j-core:2.8.2")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_PASSWORD')
    publications = ['mavenPublication']
    pkg {
        name = 'elastic-java-rest-client'
        repo = 'maven'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/larousso/elastic-java-rest-client.git'
        publish = true
        version {
            name = "${project.version}"
        }
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId 'com.adelegue'
            artifactId 'elastic-java-rest-client'
            version "${project.version}"
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

release {
    dependsOn build
    push = true
    versionSuffix = '-SNAPSHOT' // '.DEV' or '' (empty) could be useful alternatives
    tagPrefix = 'v' // 'r' or '' (empty) could be useful alternatives
}
